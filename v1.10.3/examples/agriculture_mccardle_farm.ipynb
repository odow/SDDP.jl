{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# The farm planning problem"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "There are four stages. The first stage is a deterministic planning stage. The\n",
    "next three are wait-and-see operational stages. The uncertainty in the three\n",
    "operational stages is a Markov chain for weather. There are three Markov\n",
    "states: dry, normal, and wet."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Inspired by R. McCardle, Farm management optimization. Masters thesis,\n",
    "University of Louisville, Louisville, Kentucky, United States of America\n",
    "(2009)."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "All data, including short variable names, is taken from that thesis."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using SDDP, HiGHS, Test\n",
    "\n",
    "function test_mccardle_farm_model()\n",
    "    S = [  # cutting, stage\n",
    "        0 1 2\n",
    "        0 0 1\n",
    "        0 0 0\n",
    "    ]\n",
    "    t = [60, 60, 245]  # days in period\n",
    "    D = [210, 210, 858]  # demand\n",
    "    q = [  # selling price per bale\n",
    "        [4.5 4.5 4.5; 4.5 4.5 4.5; 4.5 4.5 4.5],\n",
    "        [5.5 5.5 5.5; 5.5 5.5 5.5; 5.5 5.5 5.5],\n",
    "        [6.5 6.5 6.5; 6.5 6.5 6.5; 6.5 6.5 6.5],\n",
    "    ]\n",
    "    b = [  # predicted yield (bales/acres) from cutting i in weather j.\n",
    "        30 75 37.5\n",
    "        15 37.5 18.25\n",
    "        7.5 18.75 9.325\n",
    "    ]\n",
    "    w = 3000  # max storage\n",
    "    C = [50 50 50; 50 50 50; 50 50 50]  # cost to grow hay\n",
    "    r = [  # Cost per bale of hay from cutting i during weather condition j.\n",
    "        [5 5 5; 5 5 5; 5 5 5],\n",
    "        [6 6 6; 6 6 6; 6 6 6],\n",
    "        [7 7 7; 7 7 7; 7 7 7],\n",
    "    ]\n",
    "    M = 60.0  # max acreage for planting\n",
    "    H = 0.0  # initial inventory\n",
    "    V = [0.05, 0.05, 0.05]  # inventory cost\n",
    "    L = 3000.0  # max demand for hay\n",
    "\n",
    "    graph = SDDP.MarkovianGraph([\n",
    "        ones(Float64, 1, 1),\n",
    "        [0.14 0.69 0.17],\n",
    "        [0.14 0.69 0.17; 0.14 0.69 0.17; 0.14 0.69 0.17],\n",
    "        [0.14 0.69 0.17; 0.14 0.69 0.17; 0.14 0.69 0.17],\n",
    "    ])\n",
    "\n",
    "    model = SDDP.PolicyGraph(\n",
    "        graph;\n",
    "        lower_bound = 0.0,\n",
    "        optimizer = HiGHS.Optimizer,\n",
    "    ) do subproblem, index\n",
    "        stage, weather = index\n",
    "        # ===================== State Variables =====================\n",
    "        # Area planted.\n",
    "        @variable(subproblem, 0 <= acres <= M, SDDP.State, initial_value = M)\n",
    "        @variable(\n",
    "            subproblem,\n",
    "            bales[i = 1:3] >= 0,\n",
    "            SDDP.State,\n",
    "            initial_value = (i == 1 ? H : 0)\n",
    "        )\n",
    "        # ===================== Variables =====================\n",
    "        @variables(subproblem, begin\n",
    "            buy[1:3] >= 0  # Quantity of bales to buy from each cutting.\n",
    "            sell[1:3] >= 0 # Quantity of bales to sell from each cutting.\n",
    "            eat[1:3] >= 0  # Quantity of bales to eat from each cutting.\n",
    "            pen_p[1:3] >= 0  # Penalties\n",
    "            pen_n[1:3] >= 0  # Penalties\n",
    "        end)\n",
    "        # ===================== Constraints =====================\n",
    "        if stage == 1\n",
    "            @constraint(subproblem, acres.out <= acres.in)\n",
    "            @constraint(subproblem, [i = 1:3], bales[i].in == bales[i].out)\n",
    "        else\n",
    "            @expression(\n",
    "                subproblem,\n",
    "                cut_ex[c = 1:3],\n",
    "                bales[c].in + buy[c] - eat[c] - sell[c] + pen_p[c] - pen_n[c]\n",
    "            )\n",
    "            @constraints(\n",
    "                subproblem,\n",
    "                begin\n",
    "                    # Cannot plant more land than previously cropped.\n",
    "                    acres.out <= acres.in\n",
    "                    # In each stage we need to meet demand.\n",
    "                    sum(eat) >= D[stage-1]\n",
    "                    # We can buy and sell other cuttings.\n",
    "                    bales[stage-1].out ==\n",
    "                    cut_ex[stage-1] + acres.in * b[stage-1, weather]\n",
    "                    [c = 1:3; c != stage - 1], bales[c].out == cut_ex[c]\n",
    "                    # There is some maximum storage.\n",
    "                    sum(bales[i].out for i in 1:3) <= w\n",
    "                    # We can only sell what is in storage.\n",
    "                    [c = 1:3], sell[c] <= bales[c].in\n",
    "                    # Maximum sales quantity.\n",
    "                    sum(sell) <= L\n",
    "                end\n",
    "            )\n",
    "        end\n",
    "        # ===================== Stage objective =====================\n",
    "        if stage == 1\n",
    "            @stageobjective(subproblem, 0.0)\n",
    "        else\n",
    "            @stageobjective(\n",
    "                subproblem,\n",
    "                1000 * (sum(pen_p) + sum(pen_n)) +\n",
    "                # cost of growing\n",
    "                C[stage-1, weather] * acres.in +\n",
    "                sum(\n",
    "                    # inventory cost\n",
    "                    V[stage-1] * bales[cutting].in * t[stage-1] +\n",
    "                    # purchase cost\n",
    "                    r[cutting][stage-1, weather] * buy[cutting] +\n",
    "                    # feed cost\n",
    "                    S[cutting, stage-1] * eat[cutting] -\n",
    "                    # sell reward\n",
    "                    q[cutting][stage-1, weather] * sell[cutting] for\n",
    "                    cutting in 1:3\n",
    "                )\n",
    "            )\n",
    "        end\n",
    "        return\n",
    "    end\n",
    "    SDDP.train(model)\n",
    "    @test SDDP.termination_status(model) == :simulation_stopping\n",
    "    @test SDDP.calculate_bound(model) â‰ˆ 4074.1391 atol = 1e-5\n",
    "end\n",
    "\n",
    "test_mccardle_farm_model()"
   ],
   "metadata": {},
   "execution_count": null
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.3"
  },
  "kernelspec": {
   "name": "julia-1.11",
   "display_name": "Julia 1.11.3",
   "language": "julia"
  }
 },
 "nbformat": 4
}
