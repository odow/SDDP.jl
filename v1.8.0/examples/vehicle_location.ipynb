{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Vehicle location"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "This problem is a version of the Ambulance dispatch problem. A hospital is\n",
    "located at 0 on the number line that stretches from 0 to 100. Ambulance bases\n",
    "are located at points 20, 40, 60, 80, and 100. When not responding to a call,\n",
    "Ambulances must be located at a base, or the hospital. In this example there\n",
    "are three ambulances."
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Example location:\n",
    "\n",
    "    H       B       B       B       B       B\n",
    "    0 ---- 20 ---- 40 ---- 60 ---- 80 ---- 100"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "Each stage, a call comes in from somewhere on the number line. The agent must\n",
    "decide which ambulance to dispatch. They pay the cost of twice the driving\n",
    "distance. If an ambulance is not dispatched in a stage, the ambulance can be\n",
    "relocated to a different base in preparation for future calls. This incurs a\n",
    "cost of the driving distance."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using SDDP\n",
    "import HiGHS\n",
    "import Test\n",
    "\n",
    "function vehicle_location_model(duality_handler)\n",
    "    hospital_location = 0\n",
    "    bases = vcat(hospital_location, [20, 40, 60, 80, 100])\n",
    "    vehicles = [1, 2, 3]\n",
    "    requests = 0:10:100\n",
    "    shift_cost(src, dest) = abs(src - dest)\n",
    "    function dispatch_cost(base, request)\n",
    "        return 2 * (abs(request - hospital_location) + abs(request - base))\n",
    "    end\n",
    "    # Initial state of emergency vehicles at bases. All ambulances start at the\n",
    "    # hospital.\n",
    "    initial_state(b, v) = b == hospital_location ? 1.0 : 0.0\n",
    "    model = SDDP.LinearPolicyGraph(\n",
    "        stages = 10,\n",
    "        lower_bound = 0.0,\n",
    "        optimizer = HiGHS.Optimizer,\n",
    "    ) do sp, t\n",
    "        # Current location of each vehicle at each base.\n",
    "        @variable(\n",
    "            sp,\n",
    "            0 <= location[b = bases, v = vehicles] <= 1,\n",
    "            SDDP.State,\n",
    "            initial_value = initial_state(b, v)\n",
    "        )\n",
    "        @variables(sp, begin\n",
    "            # Which vehicle is dispatched?\n",
    "            0 <= dispatch[bases, vehicles] <= 1, Bin\n",
    "            # Shifting vehicles between bases: [src, dest, vehicle]\n",
    "            0 <= shift[bases, bases, vehicles] <= 1, Bin\n",
    "        end)\n",
    "        # Flow of vehicles in and out of bases:\n",
    "        @expression(\n",
    "            sp,\n",
    "            base_balance[b in bases, v in vehicles],\n",
    "            location[b, v].in - dispatch[b, v] - sum(shift[b, :, v]) +\n",
    "            sum(shift[:, b, v])\n",
    "        )\n",
    "        @constraints(\n",
    "            sp,\n",
    "            begin\n",
    "                # Only one vehicle dispatched to call.\n",
    "                sum(dispatch) == 1\n",
    "                # Can only dispatch vehicle from base if vehicle is at that base.\n",
    "                [b in bases, v in vehicles],\n",
    "                dispatch[b, v] <= location[b, v].in\n",
    "                # Can only shift vehicle if vehicle is at that src base.\n",
    "                [b in bases, v in vehicles],\n",
    "                sum(shift[b, :, v]) <= location[b, v].in\n",
    "                # Can only shift vehicle if vehicle is not being dispatched.\n",
    "                [b in bases, v in vehicles],\n",
    "                sum(shift[b, :, v]) + dispatch[b, v] <= 1\n",
    "                # Can't shift to same base.\n",
    "                [b in bases, v in vehicles], shift[b, b, v] == 0\n",
    "                # Update states for non-home/non-hospital bases.\n",
    "                [b in bases[2:end], v in vehicles],\n",
    "                location[b, v].out == base_balance[b, v]\n",
    "                # Update states for home/hospital bases.\n",
    "                [v in vehicles],\n",
    "                location[hospital_location, v].out ==\n",
    "                base_balance[hospital_location, v] + sum(dispatch[:, v])\n",
    "            end\n",
    "        )\n",
    "        SDDP.parameterize(sp, requests) do request\n",
    "            @stageobjective(\n",
    "                sp,\n",
    "                sum(\n",
    "                    # Distance to travel from base to emergency and then to hospital.\n",
    "                    dispatch[b, v] * dispatch_cost(b, request) +\n",
    "                    # Distance travelled by vehicles relocating bases.\n",
    "                    sum(\n",
    "                        shift_cost(b, dest) * shift[b, dest, v] for\n",
    "                        dest in bases\n",
    "                    ) for b in bases, v in vehicles\n",
    "                )\n",
    "            )\n",
    "        end\n",
    "    end\n",
    "    if get(ARGS, 1, \"\") == \"--write\"\n",
    "        # Run `$ julia vehicle_location.jl --write` to update the benchmark\n",
    "        # model directory\n",
    "        model_dir = joinpath(@__DIR__, \"..\", \"..\", \"..\", \"benchmarks\", \"models\")\n",
    "        SDDP.write_to_file(\n",
    "            model,\n",
    "            joinpath(model_dir, \"vehicle_location.sof.json.gz\");\n",
    "            test_scenarios = 100,\n",
    "        )\n",
    "        exit(0)\n",
    "    end\n",
    "    SDDP.train(\n",
    "        model;\n",
    "        iteration_limit = 20,\n",
    "        log_frequency = 10,\n",
    "        cut_deletion_minimum = 100,\n",
    "        duality_handler = duality_handler,\n",
    "    )\n",
    "    Test.@test SDDP.calculate_bound(model) >= 1000\n",
    "    return\n",
    "end\n",
    "\n",
    "# TODO(odow): find out why this fails\n",
    "# vehicle_location_model(SDDP.ContinuousConicDuality())"
   ],
   "metadata": {},
   "execution_count": null
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  },
  "kernelspec": {
   "name": "julia-1.10",
   "display_name": "Julia 1.10.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}
