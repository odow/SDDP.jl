{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Generation expansion"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using SDDP\n",
    "import HiGHS\n",
    "import Test\n",
    "\n",
    "function generation_expansion(duality_handler)\n",
    "    build_cost = 1e4\n",
    "    use_cost = 4\n",
    "    num_units = 5\n",
    "    capacities = ones(num_units)\n",
    "    demand_vals =\n",
    "        0.5 * [\n",
    "            5 5 5 5 5 5 5 5\n",
    "            4 3 1 3 0 9 8 17\n",
    "            0 9 4 2 19 19 13 7\n",
    "            25 11 4 14 4 6 15 12\n",
    "            6 7 5 3 8 4 17 13\n",
    "        ]\n",
    "    # Cost of unmet demand\n",
    "    penalty = 5e5\n",
    "    # Discounting rate\n",
    "    rho = 0.99\n",
    "    model = SDDP.LinearPolicyGraph(\n",
    "        stages = 5,\n",
    "        lower_bound = 0.0,\n",
    "        optimizer = HiGHS.Optimizer,\n",
    "    ) do sp, stage\n",
    "        @variable(\n",
    "            sp,\n",
    "            0 <= invested[1:num_units] <= 1,\n",
    "            SDDP.State,\n",
    "            Int,\n",
    "            initial_value = 0\n",
    "        )\n",
    "        @variables(sp, begin\n",
    "            generation >= 0\n",
    "            unmet >= 0\n",
    "            demand\n",
    "        end)\n",
    "\n",
    "        @constraints(\n",
    "            sp,\n",
    "            begin\n",
    "                # Can't un-invest\n",
    "                investment[i in 1:num_units], invested[i].out >= invested[i].in\n",
    "                # Generation capacity\n",
    "                sum(capacities[i] * invested[i].out for i in 1:num_units) >=\n",
    "                generation\n",
    "                # Meet demand or pay a penalty\n",
    "                unmet >= demand - sum(generation)\n",
    "                # For fewer iterations order the units to break symmetry, units are identical (tougher numerically)\n",
    "                [j in 1:(num_units-1)], invested[j].out <= invested[j+1].out\n",
    "            end\n",
    "        )\n",
    "        # Demand is uncertain\n",
    "        SDDP.parameterize(ω -> JuMP.fix(demand, ω), sp, demand_vals[stage, :])\n",
    "\n",
    "        @expression(\n",
    "            sp,\n",
    "            investment_cost,\n",
    "            build_cost *\n",
    "            sum(invested[i].out - invested[i].in for i in 1:num_units)\n",
    "        )\n",
    "        @stageobjective(\n",
    "            sp,\n",
    "            (investment_cost + generation * use_cost) * rho^(stage - 1) +\n",
    "            penalty * unmet\n",
    "        )\n",
    "    end\n",
    "    if get(ARGS, 1, \"\") == \"--write\"\n",
    "        # Run `$ julia generation_expansion.jl --write` to update the benchmark\n",
    "        # model directory\n",
    "        model_dir = joinpath(@__DIR__, \"..\", \"..\", \"..\", \"benchmarks\", \"models\")\n",
    "        SDDP.write_to_file(\n",
    "            model,\n",
    "            joinpath(model_dir, \"generation_expansion.sof.json.gz\");\n",
    "            test_scenarios = 100,\n",
    "        )\n",
    "        exit(0)\n",
    "    end\n",
    "    SDDP.train(model; log_frequency = 10, duality_handler = duality_handler)\n",
    "    Test.@test SDDP.calculate_bound(model) ≈ 2.078860e6 atol = 1e3\n",
    "    return\n",
    "end\n",
    "\n",
    "generation_expansion(SDDP.ContinuousConicDuality())\n",
    "generation_expansion(SDDP.LagrangianDuality())"
   ],
   "metadata": {},
   "execution_count": null
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.3"
  },
  "kernelspec": {
   "name": "julia-1.10",
   "display_name": "Julia 1.10.3",
   "language": "julia"
  }
 },
 "nbformat": 4
}
